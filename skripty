
01 soucet srazek = 
-- Respektuji filtry vizualu a nepridavam dalsi
SUM(data[srazky])



02 suma srazek ruzyne = 

-- respektuji filtry vizualu a pridavam dalsi

--krok2: Funkce SUMX() agreguje sloupec [srazky] tabulky vracene funkci FILTER()
SUMX(
    --krok1: Funkce FILTER() prebira tabulku data filtrovanou vizualem
    
    FILTER(data
    -- nastaveni filtru na sloupec [lokalita]
    ,data[lokalita] = "RUZYNE"),
data[srazky])




03 suma srazek vsechny roky = 

--krok2: SUMX() agreguje tabulku vracenou funkci ALL()
SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
    ALL(data),
    data[srazky])
    
    
    
  04 suma srazek 2016 = 

--krok3: Funkce SUMX() agreguje sloupec [srazky] tabulky vracene funkci FILTER()
SUMX(
    --krok2: Funkce FILTER() nastavuje podminku a vraci tabulku
    FILTER(
        -- krok1: Funkce ALL() vraci kopii tabulky data zbavenou vsech filtru
        ALL(data),
    data[rok] = 2016),
    data[srazky])




12 pocet tropickych dnu ruzyne = 
-- v danem roce
-- teplota > 30
-- srazky = 0
COUNTX(
    FILTER(data,
    data[teplota] > 30
    &&data[srazky] = 0
    &&data[lokalita] = "RUZYNE"),
data[datum])



13 pocet dnu v ruzyne teplota pod 0 = 

COUNTX(
    FILTER(data,
    data[teplota] <0
    && data[lokalita] = "RUZYNE"),
data[datum])




18 suma srazek v danem roce a minimalnim roce = 

-- suma srazek v danem roce a dane lokalite
-- minus suma srazek v minimalnim roce v dane lokalite
-- suma srazek v danem roce a lokalite
SUM(data[srazky])
- 
SUMX(
    FILTER(
        ALL(data),
    data[lokalita] = SELECTEDVALUE(data[lokalita])
    -- ROK SE ROVNÁ MINIMÁLNÍMU ROKU PRO DANOU LOKALITU
    &&data[rok] =
    MINX(
        FILTER(
        ALL(data),
    data[lokalita] = SELECTEDVALUE(data[lokalita])),
    data[rok])),
data[srazky])



17 suma srazek mezirocne = 
SUM(data[srazky])
- 
SUMX(
    FILTER(
        ALL(data),
    data[lokalita] = SELECTEDVALUE(data[lokalita])
    && data[rok] =  SELECTEDVALUE(data[rok]) - 1),
data[srazky])



BONUS = 
 -- Maximální teplota v dané lokalitě v posledním měsíci maximálního roku (napříč lokalitami) 

 MAXX(
     FILTER(
         ALL(data),
    data[lokalita] = SELECTEDVALUE(data[lokalita])
    -- maximalni rok ze vsech let
    && data[rok] = maxx(all(data),data[rok])
    && data[mesic] = 
    -- posledni mesic posledniho roku
    MAXX(
        FILTER(
        ALL(data),
    data[rok] = maxx(all(data),data[rok])),
    data[mesic])),
data[teplota])
        
